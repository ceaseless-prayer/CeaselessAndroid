buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

plugins {
    id 'com.onesignal.androidsdk.onesignal-gradle-plugin' version '0.8.1'
}

apply plugin: 'checkstyle'
apply plugin: 'com.android.application'
apply plugin: 'findbugs'
apply plugin: 'io.fabric'
apply plugin: 'pmd'
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'
apply plugin: 'realm-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven { url 'https://maven.google.com' }
}

Properties localProps = new Properties()
File propsFile = new File('./local.properties')
if (propsFile.isFile()) {
    localProps.load(new FileInputStream(file('../local.properties')))
    if (localProps['keystore.file']) {
        def keystoreFile = file(localProps['keystore.file'])
        def keystorePassword = localProps['keystore.password']
        def keystoreKeyPassword = localProps['keystore.keyPassword']

        android {
            signingConfigs {
                production {
                    keyAlias 'production'
                    storeFile keystoreFile
                    storePassword keystorePassword
                    keyPassword keystoreKeyPassword
                }
            }

            buildTypes {
                signedRelease {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                    signingConfig signingConfigs.production
                }
            }
        }
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.theotech.ceaselessandroid"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 20
        versionName "1.0.6"
        manifestPlaceholders = [onesignal_app_id               : "3d054882-fde3-4bee-bf8e-723087fc3536",
                                // Project number pulled from dashboard, local value is ignored.
                                onesignal_google_project_number: "REMOTE"]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }
    lintOptions {
        disable 'PrivateResource',
                'MissingTranslation',
                'AllowBackup',
                'UselessLeaf',
                'UselessParent',
                'IconColors',
                'IconLocation',
                'IconDensities',
                'ButtonStyle',
                'HardcodedText',
                'RtlSymmetry',
                'RtlSymmetry',
                'RtlHardcoded'
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
    implementation "com.android.support:appcompat-v7:$supportlib_version"
    implementation "com.android.support:design:$supportlib_version"
    implementation "com.android.support:cardview-v7:$supportlib_version"
    implementation "com.android.support:recyclerview-v7:$supportlib_version"
    implementation 'com.github.codechimp-org.apprater:library:1.0.30'
    implementation 'com.google.guava:guava:20.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.joanzapata.iconify:android-iconify-fontawesome:2.1.0'
    implementation 'com.makeramen:roundedimageview:2.2.1'
    implementation 'com.splitwise:tokenautocomplete:2.0.7@aar'
    implementation 'com.squareup.okhttp:okhttp:2.4.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
    implementation 'jp.wasabeef:picasso-transformations:2.1.1'
    implementation 'com.google.android.gms:play-services-analytics:11.8.0'
    implementation 'com.commonsware.cwac:merge:1.1.+'
    implementation "com.android.support:support-v4:$supportlib_version"
    implementation 'com.onesignal:OneSignal:[3.9.3, 3.99.99]'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
}

task checkstyle(type: Checkstyle) {
    configProperties.checkstyleSuppressionsPath = new File(projectDir, "config/checkstyle/suppressions.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

task findbugs(type: FindBugs, dependsOn: "assembleDebug") {
    ignoreFailures = false
    effort = "max"
    reportLevel = "low"
    classes = files(new File(buildDir, "intermediates/classes/debug/org/theotech/ceaselessandroid/"))
    excludeFilter = new File(projectDir, "config/findbugs/exclude-filter.xml")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination = new File(buildDir, "reports/findbugs/findbugs.xml")
        }
        html {
            destination = new File(buildDir, "reports/findbugs/findbugs.html")
        }
    }

    classpath = files()
}


task pmd(type: Pmd, dependsOn: "assembleDebug") {
    ignoreFailures = false
    ruleSetFiles = files(new File(projectDir, "config/pmd/pmd-ruleset.xml"))
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination = new File(buildDir, "reports/pmd/pmd.xml")
        }
        html {
            destination = new File(buildDir, "reports/pmd/pmd.html")
        }
    }
}

check.dependsOn 'checkstyle', 'findbugs', 'lint', 'pmd'

apply plugin: 'com.google.gms.google-services'
